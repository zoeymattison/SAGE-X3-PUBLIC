$ACTION
#If GUSER="MOZM" : Infbox ACTION : Endif
    Case ACTION
        When "DEBUT" : Gosub DEBUT
        When "OK" : Gosub OK
        When "END" : Gosub ZEND
        When default
    Endcase
Return

$DEBUT
Local File PORDER[ZPO]
Local Shortint NOL
Columns [ZPO] (CREDAT, POHNUM, POHFCY, BPSNUM, ZPOSTA, TOTORD, TTVORD, CREUSR, CUR, CLEFLG, BETFCY, BPRPAY)
Raz [M:ZPA]
[L]NOL = 1
Call TEMPON("R") From GESECRAN
For [ZPO] Where ZPOSTA = 1 and CLEFLG = 1 and BETFCY = 1 and BPRPAY = 'V5555'
    [M:ZPA]ZPOSTA(NOL-1) = [F:ZPO]ZPOSTA
    [M:ZPA]CREDAT(NOL-1) = [F:ZPO]CREDAT
    [M:ZPA]POHNUM(NOL-1) = [F:ZPO]POHNUM
    [M:ZPA]POHFCY(NOL-1) = [F:ZPO]POHFCY
    [M:ZPA]BPSNUM(NOL-1) = [F:ZPO]BPSNUM
    [M:ZPA]BPSNAM(NOL-1) = func AFNC.INTFLD("BPSUPPLIER","BPSNAM",[F:ZPO]BPSNUM)
    [M:ZPA]TOTORD(NOL-1) = [F:ZPO]TOTORD
    [M:ZPA]TTVORD(NOL-1) = [F:ZPO]TTVORD
    [M:ZPA]CUR(NOL-1)    = [F:ZPO]CUR
    [M:ZPA]CREUSR(NOL-1) = [F:ZPO]CREUSR
    [L]NOL+=1
    If NOL>dim([M:ZPA]ZPOSTA)
        GMESSAGE=mess(26,100,1) : GERR=2 : Break
    Endif
Next
Call TEMPOFF From GESECRAN
[M:ZPA]NBLIG = [L]NOL-1
If [M:ZPA]NBLIG=0
  GMESSAGE=mess(99,100,1) : GERR=1 : FIN=1
Endif
Affzo [ZPA]1-500
Columns [ZPO]
LogicClose File [ZPO]
Call OUVRE_TRACE("PO Approval Log for "+num$(date$)) from LECFIC
Call ECR_TRACE("GENERATION OF PO APPROVALS",0) From GESECRAN
Call ECR_TRACE("----------------------------",0) From GESECRAN
Return

$OK
Local Integer ZI : [L]ZI = 0
Local File PORDER[ZPO]
Columns [ZPO] (POHNUM, ZPOSTA, ZPOAPD, ZPOAPU)
For i = 0 to [M:ZPA]NBLIG-1
    If [M:ZPA]ZPOSTA(i) = 2
        Read [ZPO]POH0 = [M:ZPA]POHNUM(i)
        If ![S]Fstat
            If adxlog <> 1 : Trbegin [ZPO] : Endif
            [F:ZPO]ZPOSTA = 2
            [F:ZPO]ZPOAPD = date$
            [F:ZPO]ZPOAPU = func AFNC.NOMUSER(GUSER)
            Rewrite [ZPO]
            If ![S]Fstat
                Call ECR_TRACE([F:ZPO]POHNUM+" successfully Approved",0) From GESECRAN
                #Infbox 'Commit'
                Commit
                [L]ZI+=1
            Else
                Call ECR_TRACE([F:ZPO]POHNUM+" approval failed due to one or more errors.",0) From GESECRAN
                #Infbox 'Rollback'
                Rollback
                [L]ZI+=1
            Endif
        Endif
    Endif
Next
Columns [ZPO]
LogicClose File [ZPO]
Call FERME_TRACE FROM LECFIC
If [L]ZI = 0 : Return : Endif
Call LEC_TRACE() FROM LECFIC
Return

$ZEND
Call FERME_TRACE FROM LECFIC
Return