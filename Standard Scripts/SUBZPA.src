$ACTION
#If GUSER="MOZM" : Infbox ACTION : Endif
    Case ACTION
        When "OUVRE" : Gosub OUVRE
        When "DEBUT" : Gosub DEBUT
        When "OK" : Gosub OK
        When "AP_CHOIX" : Gosub AP_CHOIX
        When "END"   : Gosub ZEND
        When default
    Endcase
Return

$OUVRE
If GUSER <> "MOZM" : Infbox "Under development" : Gosub FIN from GSAISIE : Endif
Global Integer ZG_MODCNT
Local Char ZL_BPSNUM(100)(1..2)
Local Integer ZL_OK
Local Char ZL_LINPUT(100)
Local Char ZL_TITLE : [L]ZL_TITLE = 'Selection criteria'
Local Char ZL_FLDTIT : [L]ZL_FLDTIT = 'BP Supplier'
Call SAICAR([L]ZL_LINPUT,[L]ZL_TITLE,[L]ZL_FLDTIT,'BPS',0,0,'',[L]ZL_OK) From GESECRAN
if [L]ZL_OK = 1 : Gosub FIN from GSAISIE : Endif
[L]ZL_BPSNUM(1) = [L]ZL_LINPUT
If [L]ZL_BPSNUM(1) = ''
    [L]ZL_BPSNUM(1) = 'V0'
    [L]ZL_BPSNUM(2) = 'V9999999'
Else
    [L]ZL_BPSNUM(2) = [L]ZL_LINPUT
Endif
Return

$DEBUT
Call OUVRE_TRACE("PO Approval Log for "+num$(date$)) from LECFIC
Call ECR_TRACE("GENERATION OF PO APPROVALS",0) From GESECRAN
Call ECR_TRACE("----------------------------",0) From GESECRAN
Gosub RELOAD
Return

$RELOAD
Local File PORDER[ZPO]
Local Shortint NOL
Columns [ZPO] (CREDAT, POHNUM, POHFCY, BPSNUM, ZPOSTA, TOTORD, TTVORD, CREUSR, CUR, CLEFLG, BETFCY, BPRPAY)
Raz [M:ZPA]
[L]NOL = 1
Call TEMPON("R") From GESECRAN
For [ZPO] Where ZPOSTA = 1 and CLEFLG = 1 and BETFCY = 1 and BPSNUM >= [L]ZL_BPSNUM(1) and BPSNUM <= [L]ZL_BPSNUM(2)
    [M:ZPA]ZPOSTA(NOL-1) = [F:ZPO]ZPOSTA
    [M:ZPA]CREDAT(NOL-1) = [F:ZPO]CREDAT
    [M:ZPA]POHNUM(NOL-1) = [F:ZPO]POHNUM
    [M:ZPA]POHFCY(NOL-1) = [F:ZPO]POHFCY
    [M:ZPA]BPSNUM(NOL-1) = [F:ZPO]BPSNUM
    [M:ZPA]BPSNAM(NOL-1) = func AFNC.INTFLD("BPSUPPLIER","BPSNAM",[F:ZPO]BPSNUM)
    [M:ZPA]TOTORD(NOL-1) = [F:ZPO]TOTORD
    [M:ZPA]TTVORD(NOL-1) = [F:ZPO]TTVORD
    [M:ZPA]CUR(NOL-1)    = [F:ZPO]CUR
    [M:ZPA]CREUSR(NOL-1) = [F:ZPO]CREUSR
    [L]NOL+=1
    If NOL>dim([M:ZPA]ZPOSTA)
        GMESSAGE=mess(26,100,1) : GERR=2 : Break
    Endif
Next
Call TEMPOFF From GESECRAN
[M:ZPA]NBLIG = [L]NOL-1
If [M:ZPA]NBLIG=0
    Infbox "No records found!"
    Columns [ZPO]
    LogicClose File [ZPO]
    Gosub FIN from GSAISIE
Endif
Affzo [ZPA]1-500
Columns [ZPO]
LogicClose File [ZPO]
Return

$AP_CHOIX
#Infbox num$(REPONSE)
Local Integer ZI_3
Case num$(REPONSE)
    When num$(2001) : [L]ZI_3 = 2 : Gosub APP_SEL
    When num$(2002) : [L]ZI_3 = 1 : Gosub APP_SEL
Endcase
Return

$APP_SEL
For i = 0 to [M:ZPA]NBLIG-1
    [M:ZPA]ZPOSTA(i) = [L]ZI_3
    Affzo [M:ZPA]ZPOSTA(i)
Next
Raz [L]ZI_3
Return

$OK
Local Integer ZI_4 : [L]ZI_4 = 0
For i = 0 to [M:ZPA]NBLIG-1
    if [M:ZPA]ZPOSTA(i) = 2
        [L]ZI_4 += 1
    Endif
Next
If [L]ZI_4 = 0 : Infbox "No records have been selected for approval!" : FIN = 0 : Return : Endif
Gosub APPROVE
Gosub RELOAD
FIN = 0
Return

$APPROVE
#Infbox num$([M:ZPA]ZPOSTA(0))
Local Integer ZI : [L]ZI = 0
Local File PORDER[ZPO]
Columns [ZPO] (POHNUM, ZPOSTA, ZPOAPD, ZPOAPU)
For i = 0 to [M:ZPA]NBLIG-1
    #Infbox "Inside"
    If [M:ZPA]ZPOSTA(i) = 2
        Read [ZPO]POH0 = [M:ZPA]POHNUM(i)
        If ![S]Fstat
            If adxlog <> 1 : Trbegin [ZPO] : Endif
            [F:ZPO]ZPOSTA = 2
            [F:ZPO]ZPOAPD = date$
            [F:ZPO]ZPOAPU = func AFNC.NOMUSER(GUSER)
            Rewrite [ZPO]
            If ![S]Fstat
                Call ECR_TRACE([F:ZPO]POHNUM+" successfully approved",0) From GESECRAN
                #Infbox 'Commit'
                Commit
                [L]ZI+=1
                [V]ZG_MODCNT += 1
            Else
                Call ECR_TRACE([F:ZPO]POHNUM+" approval failed due to one or more errors",0) From GESECRAN
                #Infbox 'Rollback'
                Rollback
                [L]ZI+=1
            Endif
        Endif
    Endif
Next
Columns [ZPO]
LogicClose File [ZPO]
Return

$ZEND
Call FERME_TRACE FROM LECFIC
If [V]ZG_MODCNT > 0 : Call LEC_TRACE() FROM LECFIC : Endif
Raz [V]ZG_MODCNT
Return